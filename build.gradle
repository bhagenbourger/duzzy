plugins {
    id 'application'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'ru.vyarus.pom' version '3.0.0' apply false
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id 'com.github.spotbugs' version '6.0.27'
    id 'jacoco'
    id 'jacoco-report-aggregation'
}

application {
    mainClass = "io.duzzy.cli.App"
}

jar.enabled = false
build.enabled = false

graalvmNative {
    binaries.configureEach {
        resources.autodetect()
    }
    toolchainDetection = false
    testSupport = true
}

startScripts {
    unixStartScriptGenerator.template = resources.text.fromFile('config/start-scripts/duzzyUnixStartScript.txt')
    windowsStartScriptGenerator.template = resources.text.fromFile('config/start-scripts/duzzyWindowsStartScript.txt')
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

ext {
    pluginTitle = ''
    pluginDescription = ''
    pluginVendor = 'Duzzy'
    avroVersion = '1.12.0'
    jacksonVersion = '2.18.0'
    log4j2Version = '2.24.3'
}

project(':plugin-avro') {
    ext {
        pluginTitle = 'Avro plugin'
        pluginDescription = 'Avro parser and avro serializer'
    }
}

project(':plugin-duckdb') {
    ext{
        pluginTitle = 'Duckdb plugin'
        pluginDescription = 'Duckdb sink'
    }
}

project(':plugin-hdfs') {
    ext{
        pluginTitle = 'Hdfs plugin'
        pluginDescription = 'Hdfs sink'
    }
}

project(':plugin-kafka') {
    ext {
        pluginTitle = 'Kafka plugin'
        pluginDescription = 'Kafka sink'
    }
}

project(':plugin-mysql') {
    ext {
        pluginTitle = 'MySQL plugin'
        pluginDescription = 'MySQL sink'
    }
}

project(':plugin-parquet') {
    ext {
        pluginTitle = 'Parquet plugin'
        pluginDescription = 'Parquet serializer'
    }
}

project(':plugin-postgresql') {
    ext {
        pluginTitle = 'Postgres plugin'
        pluginDescription = 'Postgres sink'
    }
}

dependencies {
    implementation project(":cli")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    group = 'io.duzzy'
    version = '0.0.0-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    checkstyle {
        toolVersion = "10.21.0"
        ignoreFailures = false
        maxWarnings = 0
    }

    spotbugs {
        toolVersion = '4.8.6'
        excludeFilter = file('../config/spotbugs/spotbugs-exclude.xml')
    }

    pmd {
        consoleOutput = true
        toolVersion = "7.10.0"
    }

    test {
        finalizedBy jacocoTestReport
    }

    repositories {
        mavenCentral()
    }
}


/**
 * Shadow jar and MANIFEST.MF for plugins
 */
subprojects {
    if (project.name.startsWith('plugin-')) {
        apply plugin: 'com.gradleup.shadow'

        jar {
            manifest {
                attributes 'Implementation-Vendor': "${pluginVendor}"
                attributes 'Implementation-Title': "${pluginTitle}"
                attributes 'Implementation-Version': project.version
                attributes 'Duzzy-Plugin-Group-Id': project.group
                attributes 'Duzzy-Plugin-Artifact-Id': project.name
                attributes 'Duzzy-Plugin-Description': "${pluginDescription}"
            }
        }
    }
}

/**
 * Dependencies for all modules
 */
subprojects {
    dependencies {
        implementation 'org.slf4j:slf4j-api:2.0.16'
        implementation "org.apache.logging.log4j:log4j-api:$log4j2Version"
        implementation "org.apache.logging.log4j:log4j-core:$log4j2Version"
        implementation "org.apache.logging.log4j:log4j-slf4j2-impl:$log4j2Version"


        testImplementation project(":tests")
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core:3.26.3'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }
}

/**
 * Publication for core and plugins
 */
subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'ru.vyarus.pom'

    maven.pom {
        name = 'Duzzy'
        description = 'Give me your schema, I\'ll give you your test data.'
        url = 'https://github.com/bhagenbourger/duzzy'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'bhagenbourger'
                name = 'Beno√Æt Hagenbourger'
                email = 'benoit@hagenbourger.fr'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/bhagenbourger/duzzy.git'
            developerConnection = 'scm:git:ssh://github.com/bhagenbourger/duzzy.git'
            url = 'https://github.com/bhagenbourger/duzzy'
        }
    }

    publishing {
        publications {
            if (project.name.equals('core')) {
                maven(MavenPublication) {
                    from components.java
                }
            }
            if (project.name.startsWith('plugin-')) {
                shadow(MavenPublication) {
                    from components.shadow
                }
            }
        }
        repositories {
            maven {
                name = 'GitHubPackages'
                url = "https://maven.pkg.github.com/bhagenbourger/duzzy"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}